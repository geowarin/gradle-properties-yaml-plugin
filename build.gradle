buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "com.gradle.publish:plugin-publish-plugin:0.9.1"
  }
}


plugins {
    id 'com.jfrog.bintray' version '1.3.1'
}

apply plugin: "com.gradle.plugin-publish"
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'idea'

sourceCompatibility = 1.7
targetCompatibility = 1.7

def releaseVersion = '0.0.2'
group = 'pl.softmate'
archivesBaseName = 'gradle-properties-yaml-plugin'
version = releaseVersion

ext {
    myGithubUrl = 'https://github.com/mwiktorczyk/gradle-properties-yaml-plugin'
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile gradleApi()
    compile 'org.yaml:snakeyaml:1.16'
    compile 'org.springframework:spring-core:4.2.1.RELEASE'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives jar, groovydocJar, sourceJar
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = project.archivesBaseName
        desc = "A Gradle plugin for loading and working with yaml properties, enhancing Gradle's built in functionality."
        licenses = ['MIT']
        websiteUrl = project.myGithubUrl
        issueTrackerUrl = "${project.myGithubUrl}/issues"
        vcsUrl = "${project.myGithubUrl}.git"
        labels = ['gradle', 'properties', 'yaml']
        publicDownloadNumbers = true
        version {
            vcsTag = "Release_${releaseVersion}"
            attributes = [
                    'gradle-plugin': project.archivesBaseName
            ]
        }
    }
}

pluginBundle {
  website = bintray.pkg.websiteUrl
  vcsUrl = bintray.pkg.vcsUrl
  description = bintray.pkg.desc
  tags = bintray.pkg.labels

  plugins {
    greetingsPlugin {
      id = 'pl.softmate.gradle-properties-yaml-plugin'
      displayName = project.archivesBaseName
    }
  }
}

